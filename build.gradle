plugins {
    id "java"
    id "maven-publish"
    id "net.kyori.blossom" version "1.2.0"
}

ext {
    branch = "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

group "org.bookmc"
version "0.2.3${branch != "main" && !branch.isEmpty() ? "-$branch" : ""}"

repositories {
    mavenCentral()
    maven {
        url "https://maven.bookmc.org/releases/"
    }
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    include "com.google.code.gson:gson:2.8.8"
    include "org.bookmc:install:0.3.1"
    include "net.sf.jopt-simple:jopt-simple:6.0-alpha-3"

    include "org.apache.logging.log4j:log4j-api:2.14.1"
    include "org.apache.logging.log4j:log4j-core:2.14.1"
}

blossom {
    replaceToken("%INSTALLER_VERSION%", project.version)
}

jar {
    from {
        configurations.include.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    manifest.attributes("Main-Class": "org.bookmc.installer.Main")
}

publishing {
    repositories {
        maven {
            def env = System.getenv()
            boolean login = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            if (login) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = !login ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                url = "https://github.com/BookMC/${project.name}"

                developers {
                    developer {
                        name = "ChachyDev"
                    }
                }
            }
        }
    }
}